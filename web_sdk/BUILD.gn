# Copyright 2019 The Flutter Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/dart/build/dart/dart_action.gni")
import("//third_party/dart/utils/compile_platform.gni")

sdk_dill = "$root_out_dir/flutter_web_sdk/kernel/flutter_ddc_sdk.dill"
sdk_libraries_json = "$flutter_root/web_sdk/libraries.json"

web_ui_sources = [
  "$flutter_root/lib/stub_ui/lib/src/compositing.dart",
  "$flutter_root/lib/stub_ui/lib/src/geometry.dart",
  "$flutter_root/lib/stub_ui/lib/src/hash_codes.dart",
  "$flutter_root/lib/stub_ui/lib/src/lerp.dart",
  "$flutter_root/lib/stub_ui/lib/src/natives.dart",
  "$flutter_root/lib/stub_ui/lib/src/painting.dart",
  "$flutter_root/lib/stub_ui/lib/src/pointer.dart",
  "$flutter_root/lib/stub_ui/lib/src/semantics.dart",
  "$flutter_root/lib/stub_ui/lib/src/text.dart",
  "$flutter_root/lib/stub_ui/lib/ui.dart",
  "$flutter_root/lib/stub_ui/lib/src/window.dart",
  "$flutter_root/lib/stub_ui/lib/src/canvas.dart",
  "$flutter_root/lib/stub_ui/lib/src/initialization.dart",
  "$flutter_root/lib/stub_ui/lib/src/pointer_binding.dart",
  "$flutter_root/lib/stub_ui/lib/src/tile_mode.dart",
  "$flutter_root/lib/stub_ui/lib/src/browser_routing/strategies.dart",
]

web_engine_sources = [
  "$flutter_root/lib/stub_ui/lib/src/assets/assets.dart",
  "$flutter_root/lib/stub_ui/lib/src/browser_routing/history.dart",
  "$flutter_root/lib/stub_ui/lib/src/compositor/engine_delegate.dart",
  "$flutter_root/lib/stub_ui/lib/src/compositor/layer_scene_builder.dart",
  "$flutter_root/lib/stub_ui/lib/src/compositor/layer_tree.dart",
  "$flutter_root/lib/stub_ui/lib/src/compositor/layer.dart",
  "$flutter_root/lib/stub_ui/lib/src/compositor/platform_message.dart",
  "$flutter_root/lib/stub_ui/lib/src/compositor/raster_cache.dart",
  "$flutter_root/lib/stub_ui/lib/src/compositor/rasterizer.dart",
  "$flutter_root/lib/stub_ui/lib/src/compositor/runtime_delegate.dart",
  "$flutter_root/lib/stub_ui/lib/src/compositor/surface.dart",
  "$flutter_root/lib/stub_ui/lib/src/compositor/viewport_metrics.dart",
  "$flutter_root/lib/stub_ui/lib/src/dom_renderer/dom_renderer.dart",
  "$flutter_root/lib/stub_ui/lib/src/semantics/checkable.dart",
  "$flutter_root/lib/stub_ui/lib/src/semantics/incrementable.dart",
  "$flutter_root/lib/stub_ui/lib/src/semantics/label_and_value.dart",
  "$flutter_root/lib/stub_ui/lib/src/semantics/scrollable.dart",
  "$flutter_root/lib/stub_ui/lib/src/semantics/semantics.dart",
  "$flutter_root/lib/stub_ui/lib/src/semantics/tappable.dart",
  "$flutter_root/lib/stub_ui/lib/src/semantics/text_field.dart",
  "$flutter_root/lib/stub_ui/lib/src/services/message_codec.dart",
  "$flutter_root/lib/stub_ui/lib/src/services/message_codecs.dart",
  "$flutter_root/lib/stub_ui/lib/src/text/font_collection.dart",
  "$flutter_root/lib/stub_ui/lib/src/text/measurement.dart",
  "$flutter_root/lib/stub_ui/lib/src/text/ruler.dart",
  "$flutter_root/lib/stub_ui/lib/src/text/unicode_range.dart",
  "$flutter_root/lib/stub_ui/lib/src/text/word_break_properties.dart",
  "$flutter_root/lib/stub_ui/lib/src/text/word_breaker.dart",
  "$flutter_root/lib/stub_ui/lib/src/alarm_clock.dart",
  "$flutter_root/lib/stub_ui/lib/src/bitmap_canvas.dart",
  "$flutter_root/lib/stub_ui/lib/src/browser_detection.dart",
  "$flutter_root/lib/stub_ui/lib/src/conic.dart",
  "$flutter_root/lib/stub_ui/lib/src/dom_canvas.dart",
  "$flutter_root/lib/stub_ui/lib/src/dom_renderer.dart",
  "$flutter_root/lib/stub_ui/lib/src/engine_canvas.dart",
  "$flutter_root/lib/stub_ui/lib/src/engine.dart",
  "$flutter_root/lib/stub_ui/lib/src/houdini_canvas.dart",
  "$flutter_root/lib/stub_ui/lib/src/html_image_codec.dart",
  "$flutter_root/lib/stub_ui/lib/src/keyboard.dart",
  "$flutter_root/lib/stub_ui/lib/src/onscreen_logging.dart",
  "$flutter_root/lib/stub_ui/lib/src/path_to_svg.dart",
  "$flutter_root/lib/stub_ui/lib/src/recording_canvas.dart",
  "$flutter_root/lib/stub_ui/lib/src/shadow.dart",
  "$flutter_root/lib/stub_ui/lib/src/text_editing.dart",
  "$flutter_root/lib/stub_ui/lib/src/util.dart",
  "$flutter_root/lib/stub_ui/lib/src/validators.dart",
  "$flutter_root/lib/stub_ui/lib/src/vector_math.dart",
]

group("web_sdk") {
  deps = [
    ":flutter_dartdevc_kernel_sdk",
    ":web_ui_library",
    ":web_ui_sources",
    ":web_engine_sources",
  ]
}

prebuilt_dart_action("web_ui_sources") {
  inputs = web_ui_sources + [ "sdk_rewriter.dart" ]

  packages = "//third_party/dart/.packages"
  script = "sdk_rewriter.dart"
  output_dir = rebase_path("$root_out_dir/flutter_web_sdk/lib/ui/")
  input_dir = rebase_path("$flutter_root/lib/stub_ui/lib/")

  args = [
    "--output-dir=$output_dir",
    "--input-dir=$input_dir",
    "--ui"
  ]

  outputs = [
    "$root_out_dir/flutter_web_engine_sdk_stamp",
  ]

  foreach(source_file, web_ui_sources) {
    path = rebase_path(source_file)
    args += [ "--input=$path" ]
  }
}

prebuilt_dart_action("web_engine_sources") {
  inputs = web_engine_sources + [ "sdk_rewriter.dart" ]

  packages = "//third_party/dart/.packages"
  script = "sdk_rewriter.dart"
  output_dir = rebase_path("$root_out_dir/flutter_web_sdk/lib/_engine/")
  input_dir = rebase_path("$flutter_root/lib/stub_ui/lib/src/")

  args = [
    "--output-dir=$output_dir",
    "--input-dir=$input_dir",
    "--engine",
  ]

  outputs = [
    "$root_out_dir/flutter_web_sdk_stamp",
  ]

  foreach(source_file, web_engine_sources) {
    path = rebase_path(source_file)
    args += [ "--input=$path" ]
  }
}

copy("web_ui_library") {
  sources = [
    "$flutter_root/web_sdk/libraries.json",
  ]

  outputs = [
    "$root_out_dir/flutter_web_sdk/{{source_file_part}}",
  ]
}

prebuilt_dart_action("flutter_dartdevc_kernel_sdk") {
  deps = [
    "//third_party/dart/pkg:pkg_files_stamp",
    "//third_party/dart/utils/dartdevc:dartdevc_files_stamp",
    "//third_party/dart/utils/dartdevc:dartdevc_sdk_patch_stamp",
  ]

  inputs = [
    "//third_party/dart/pkg/dev_compiler/tool/kernel_sdk.dart",
  ]

  outputs = [
    sdk_dill,
    "$root_out_dir/flutter_web_sdk/lib/_internal/libraries.json",
    "$root_out_dir/flutter_web_sdk/kernel/amd/dart_sdk.js",
    "$root_out_dir/flutter_web_sdk/kernel/amd/dart_sdk.js.map",
  ]

  script = "flutter_kernel_sdk.dart"
  packages = "//third_party/dart/.packages"

  output_path = rebase_path(sdk_dill)
  libraries_path = rebase_path(sdk_libraries_json)

  args = [
    "--output=$output_path",
    "--libraries=$libraries_path",
  ]
}
