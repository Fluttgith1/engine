# Copyright 2013 The Flutter Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

source_set("testing_lib") {
  testonly = true

  sources = [
    "//flutter/testing/assertions.h",
    "//flutter/testing/testing.cc",
    "//flutter/testing/testing.h",
  ]

  public_deps = [
    "//flutter/fml",
    "//third_party/googletest:gtest",
  ]
  public_configs = [ "//flutter:config" ]
}

source_set("testing") {
  testonly = true

  sources = [
    "//flutter/testing/run_all_unittests.cc",
    "//flutter/testing/thread_test.cc",
    "//flutter/testing/thread_test.h",
  ]

  public_deps = [
    ":testing_lib",
  ]
}

source_set("dart") {
  testonly = true

  sources = [
    "//flutter/testing/test_dart_native_resolver.cc",
    "//flutter/testing/test_dart_native_resolver.h",
  ]

  public_deps = [
    ":testing",
    "//flutter/runtime:libdart",
    "//third_party/tonic",
  ]
}

source_set("skia") {
  testonly = true

  sources = [
    "//flutter/testing/assertions_skia.h",
  ]

  public_deps = [
    ":testing_lib",
    "//third_party/skia",
  ]
}

if (current_toolchain == host_toolchain) {
  source_set("opengl") {
    testonly = true

    configs += [ "//third_party/swiftshader_flutter:swiftshader_config" ]

    sources = [
      "//flutter/testing/test_gl_surface.cc",
      "//flutter/testing/test_gl_surface.h",
    ]

    deps = [
      ":skia",
      "//flutter/fml",
      "//third_party/swiftshader_flutter:swiftshader",
    ]
  }
}
