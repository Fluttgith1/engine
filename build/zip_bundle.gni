# Copyright 2013 The Flutter Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

template("zip_bundle") {
  action(target_name) {
    script = "//flutter/build/zip.py"
    args = [ "-o", rebase_path(invoker.output) ]
    inputs = []
    outputs = [ invoker.output ]
    deps = invoker.deps

    foreach(input, invoker.files) {
      args += [
        "-i",
        rebase_path(input.source),
        rebase_path(input.destination),
      ]
      inputs += [ input.source ]
    }
  }
}
