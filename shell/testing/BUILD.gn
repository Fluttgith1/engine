# Copyright 2013 The Flutter Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//flutter/common/config.gni")

if (flutter_runtime_mode == "debug") {
  executable("testing") {
    output_name = "flutter_tester"

    public_configs = [
      "//flutter:config",
      "//flutter:export_dynamic_symbols",
    ]

    sources = [ "tester_main.cc" ]
    if (is_win) {
      libs = [
        "psapi.lib",
        "user32.lib",
        "FontSub.lib",
        "shlwapi.lib",
      ]
    }

    deps = [
      "//flutter/assets",
      "//flutter/common",
      "//flutter/flow",
      "//flutter/fml",
      "//flutter/fml/trace",
      "//flutter/lib/snapshot",
      "//flutter/shell/common",
      "//flutter/shell/gpu:gpu_surface_software",
      "//third_party/dart/runtime/bin:dart_io_api",
      "//third_party/skia",
    ]
  }
} else {
  group("testing") {
    # The Flutter Dart tester is only available in JIT runtime modes.
  }
}
