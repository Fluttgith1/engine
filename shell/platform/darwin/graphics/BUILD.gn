# Copyright 2013 The Flutter Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

assert(is_ios)

import("//flutter/common/config.gni")
import("//flutter/shell/gpu/gpu.gni")
import("//flutter/shell/platform/darwin/common/framework_shared.gni")

shell_gpu_configuration("ios_gpu_configuration") {
  enable_software = true
  enable_gl = true
  enable_vulkan = false
  enable_metal = shell_enable_metal
}

source_set("flutter_ios_graphics") {
  cflags_objc = flutter_cflags_objc
  cflags_objcc = flutter_cflags_objcc
  defines = []

  sources = [
    "ios_context.h",
    "ios_context.mm",
    "ios_context_gl.h",
    "ios_context_gl.mm",
    "ios_context_software.h",
    "ios_context_software.mm",
    "ios_external_texture_gl.h",
    "ios_external_texture_gl.mm",
    "ios_render_target_gl.h",
    "ios_render_target_gl.mm",
    "ios_switchable_gl_context.h",
    "ios_switchable_gl_context.mm",
    "rendering_api_selection.h",
    "rendering_api_selection.mm",
  ]

  if (shell_enable_metal) {
    defines += [ "FLUTTER_SHELL_ENABLE_METAL=1" ]

    sources += [
      "ios_context_metal.h",
      "ios_context_metal.mm",
      "ios_external_texture_metal.h",
      "ios_external_texture_metal.mm",
    ]
  }

  deps = [
    ":ios_gpu_configuration",
    "//flutter/fml",
    "//flutter/common/graphics",
    "//flutter/shell/platform/darwin/common:framework_shared",
    "//third_party/skia",
  ]

  libs = [
    "CoreMedia.framework",
    "CoreVideo.framework",
    "OpenGLES.framework",
    "QuartzCore.framework",
  ]
}
