# Copyright 2013 The Flutter Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

assert(is_ios || is_mac)

import("//flutter/common/config.gni")
import("//flutter/impeller/tools/impeller.gni")
import("//flutter/shell/gpu/gpu.gni")

shell_gpu_configuration("darwin_gpu_configuration") {
  enable_software = true
  enable_gl = is_mac
  enable_vulkan = false
  enable_metal = shell_enable_metal
}

source_set("graphics") {
  cflags_objc = flutter_cflags_objc_arc
  cflags_objcc = flutter_cflags_objcc_arc

  sources = [
    "FlutterDarwinContextMetalSkia.h",
    "FlutterDarwinContextMetalSkia.mm",
    "FlutterDarwinExternalTextureMetal.h",
    "FlutterDarwinExternalTextureMetal.mm",
  ]

  deps = [
    "//flutter/common/graphics",
    "//flutter/display_list",
    "//flutter/fml",
    "//flutter/shell/common",
    "//flutter/shell/platform/darwin/common:framework_shared",
  ]

  if (impeller_supports_rendering) {
    sources += [
      "FlutterDarwinContextMetalImpeller.h",
      "FlutterDarwinContextMetalImpeller.mm",
    ]
    deps += [ "//flutter/impeller" ]
  }

  frameworks = [ "CoreVideo.framework" ]

  public_deps = [ "//third_party/skia" ]

  public_configs = [ "//flutter:config" ]
}

source_set("graphics_embedder_api") {
  cflags_objc = flutter_cflags_objc_arc
  cflags_objcc = flutter_cflags_objcc_arc

  sources = [
    "FlutterExternalTexture.h",
    "FlutterExternalTexture.mm",
    "FlutterSurface.h",
    "FlutterSurface.mm",
    "FlutterSurfaceManager.h",
    "FlutterSurfaceManager.mm",
  ]

  deps = [
    ":darwin_gpu_configuration",
    ":graphics",
    "//flutter/shell/platform/embedder:embedder_as_internal_library",
  ]

  frameworks = [
    "CoreVideo.framework",
    "QuartzCore.framework",
    "IOSurface.framework",
  ]

  public_configs = [ "//flutter:config" ]
}
