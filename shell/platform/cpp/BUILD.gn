# Copyright 2013 The Flutter Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("$flutter_root/common/config.gni")

_common_defines = [ "FLUTTER_DESKTOP_LIBRARY" ]

_public_headers = [
  "public/flutter_export.h",
  "public/flutter_messenger.h",
  "public/flutter_plugin_registrar.h",
]
if (use_glfw_desktop) {
  _public_headers += [ "public/flutter_glfw.h" ]
}

# The headers are a separate source set since the client wrapper is allowed
# to depend on the public headers, but none of the rest of the code.
source_set("common_cpp_library_headers") {
  public = _public_headers

  defines = _common_defines
}

source_set("common_cpp") {
  public = [
    "incoming_message_dispatcher.h",
    "text_input_model.h",
  ]

  sources = [
    "incoming_message_dispatcher.cc",
    "text_input_model.cc",
  ]

  defines = _common_defines + [ "USE_RAPID_JSON" ]

  deps = [
    ":common_cpp_library_headers",
    "$flutter_root/shell/platform/embedder:embedder",
  ]

  if (use_glfw_desktop) {
    sources += [
      "glfw/flutter_glfw.cc",
      "glfw/key_event_handler.cc",
      "glfw/key_event_handler.h",
      "glfw/keyboard_hook_handler.h",
      "glfw/text_input_plugin.cc",
      "glfw/text_input_plugin.h",
    ]

    deps += [
      "client_wrapper:client_wrapper",
      "//third_party/rapidjson",
    ]

    if (is_linux) {
      libs = [ "GL" ]

      configs += [
        "$flutter_root/shell/platform/linux/config:epoxy",
        "$flutter_root/shell/platform/linux/config:glfw3",
        "$flutter_root/shell/platform/linux/config:gtk3",
        "$flutter_root/shell/platform/linux/config:x11",
      ]
    }
  }
}

copy("publish_headers") {
  sources = _public_headers
  outputs = [
    "$root_out_dir/{{source_file_part}}",
  ]
}
