# Copyright 2013 The Flutter Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//flutter/common/config.gni")
import("//flutter/impeller/tools/impeller.gni")
import("//flutter/shell/config.gni")

common_deps = [
  "//flutter/common",
  "//flutter/common/graphics",
  "//flutter/flow",
  "//flutter/fml",
  "//flutter/shell/common",
  "//flutter/skia",
]

source_set("software") {
  sources = [
    "surface_software.cc",
    "surface_software.h",
    "surface_software_delegate.cc",
    "surface_software_delegate.h",
  ]

  public_deps = common_deps
}

source_set("gl") {
  sources = [
    "surface_gl_delegate.cc",
    "surface_gl_delegate.h",
    "surface_gl_skia.cc",
    "surface_gl_skia.h",
  ]

  public_deps = common_deps

  if (impeller_enable_opengles) {
    sources += [
      "surface_gl_impeller.cc",
      "surface_gl_impeller.h",
    ]

    public_deps += [ "//flutter/impeller" ]
  }
}

source_set("vulkan") {
  sources = [
    "surface_vulkan.cc",
    "surface_vulkan.h",
    "surface_vulkan_delegate.cc",
    "surface_vulkan_delegate.h",
  ]
  public_deps = common_deps + [
                  "//flutter/shell/platform/embedder:embedder_headers",
                  "//flutter/vulkan",
                  "//flutter/vulkan/procs",
                ]

  if (impeller_enable_vulkan) {
    sources += [
      "surface_vulkan_impeller.cc",
      "surface_vulkan_impeller.h",
    ]

    public_deps += [ "//flutter/impeller" ]
  }
}

source_set("metal") {
  sources = [
    "surface_metal_delegate.cc",
    "surface_metal_delegate.h",
    "surface_metal_skia.h",
    "surface_metal_skia.mm",
  ]

  public_deps = common_deps

  if (impeller_enable_metal) {
    sources += [
      "surface_metal_impeller.h",
      "surface_metal_impeller.mm",
    ]

    public_deps += [ "//flutter/impeller" ]
  }
}

if (is_mac) {
  impeller_component("metal_unittests") {
    testonly = true
    target_type = "executable"
    sources = [ "surface_metal_impeller_unittests.mm" ]

    frameworks = [
      "AppKit.framework",
      "QuartzCore.framework",
    ]

    deps = [
             "//flutter/testing",
             ":metal",
             "//flutter/impeller/fixtures",
             "//flutter/fml",
             "//flutter/runtime",
             "//flutter/runtime:libdart",
             "//flutter/testing",
           ] + common_deps
  }
}
